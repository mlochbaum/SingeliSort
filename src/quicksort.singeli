include './partition'
include './median'
include './xorshift'
include './arith'  # Logs and square roots

def min_i32 = i32 ~~ (-1<<31)

def get_pivot{array:pT, n:U, getaux, sort} = {
  # log_2 of minimum size for sampling
  def sl0 = 14
  # Output array and index
  arr:=array; ind:U = 0
  if (n <= 256) {
    ind = locate_3_median{array, n}
  } else if (n <= 1024) {
    ind = locate_3of3_pseudomedian{array, n}
  } else if (n <= 1 << sl0) {
    ind = locate_5of3_pseudomedian{array, n}
  } else {
    aux := getaux{}
    # gap is the expected distance between adjacent samples
    # We'll get about n/gap samples
    log2:U = floor_log2{n, sl0}
    gap_min := 1 << (log2 / 2 + floor_log2{2 + sl0} >> 1)
    gap := sqrt_approx{2*n * (2+log2), gap_min}

    # Collect samples with split xorshift and add to aux
    aux1 := aux
    def add{ind} = { aux1 <- array->ind; ++aux1 }
    mask := gap_min - 1
    def add3 = make_split_xorshift{tup{13,17,5}, n, mask, add}

    i:U = 0; while (i < n - (mask + 2 * gap)) add3{i, gap}
    ns := aux1 - aux
    sort{aux, aux, aux1, ns, ns*(width{eltype{pT}}/8), min_i32, 0}
    arr = aux
    ind = ns / 2
  }
  arr -> ind
}

# Quicksort loop with unstable partitioning
# Pointer layout credit to Scandum: https://github.com/scandum/fluxsort
# Sort src, placing the result in dst and using space from aux
# src may be equal to dst or aux
def flux_loop{recur, tailcall, piv:T, src:*T, dst:*T, aux:*T, n:U, aux_bytes:U, min:T, has_max:u1} = {

  # Partition: left side directly in dst with length l, includes pivots
  l := flux_partition{src, <=, piv, dst, aux, n}

  # Right-hand side
  # If pivot is the maximum value, everything ends in dst
  if (l == n) {
    # Partition again with pivots on the right
    # This bounds performance by O(k*n) for only k unique values
    l = flux_partition{dst, <, piv, dst, aux, n}
    set{dst+l, aux, n-l} # Should probably write a reverse partition to avoid this
  } else {
    # Otherwise, sort the right-hand side, moving it from aux to dst
    recur{aux, dst+l, aux, n-l, aux_bytes, piv, has_max}
  }

  # Left-hand side
  if (tailcall) { # Another call on the original arguments follows
    src = dst
    n = l
    has_max = 1
  } else {
    recur{dst, dst, aux, l, aux_bytes, min, 1}
  }
}

flux32loop(src:*i32, dst:*i32, aux:*i32, n:u64, aux_bytes:u64, min:i32, has_max:u1) : void = {
  while (u1~~1) {
    # Base case
    if (n < 32) {
      sort_lt32{dst, src, n}
      return{}
    }

    if (has_max) {
      # Distribution base cases
      range := u32 ~~ (dst->n - min)
      if (u64^~(range/4) < n and u64^~range < aux_bytes/(width{u64}/8)) {
        # Always sort in place on dst
        # Counting sort could have a different src/dst, but if src isn't
        # dst then it's equal to aux, and we need that space
        if (src != dst) set{dst, src, n}
        count_sort{dst, u32<~n, *u32~~aux, min, range + 1}
        return{}
      }
      if (n <= (1<<16) and range < (1<<16)) {
        radpack32(*u32~~src, *u32~~dst, u32<~n, *void~~aux, u32~~min)
        return{}
      }
    }

    def recur = bind{call,flux32loop}
    def getaux{} = { a:=aux; if (a==src) a=dst; a }
    piv := get_pivot{src, n, getaux, recur}
    flux_loop{recur,1, piv, src,dst,aux,n,aux_bytes, min,has_max}
  }
}

flux32(x:*i32, n:u64, aux:*void, aux_bytes:u64) : void = {
  flux32loop(x, x, *i32~~aux, n, aux_bytes, min_i32, 0)
}
