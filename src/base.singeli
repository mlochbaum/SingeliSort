include 'arch/c'
include 'skin/c'

def ptrwidth = width{*void}
def usize = primtype{'u',ptrwidth}

def to_ptrdiff{a} = cast{primtype{'i',ptrwidth}, a}
def to_ptrdiff{a:T} = promote{ptrwidth, a}
def __add{a,b:P & match{'pointer',typekind{P}}}  = emit{P, 'op +', to_ptrdiff{a}, b}
def __add{a:P,b & match{'pointer',typekind{P}}}  = emit{P, 'op +', a, to_ptrdiff{b}}
def __sub{a:P,b & match{'pointer',typekind{P}}}  = emit{P, 'op -', a, to_ptrdiff{b}}
def __sub{a:P,b:P & match{'pointer',typekind{P}}}  = emit{usize, 'op -', a, b}

oper infix right -> load 50
oper infix right <- __store 5
def __store{ind}{ptr, val} = store{ptr, ind, val}
def __store{ptr, val} = __store{0}{ptr, val}

def addtype{a:T} = T
def addtype{a:T & match{'pointer',typekind{T}}} = usize
def __add{a, b:u1} = __add{a, promote{addtype{a},b}}
def __sub{a, b:u1} = __sub{a, promote{addtype{a},b}}

def LIKELY{X} = emit{type{X}, '__builtin_expect', X,1}
def RARE  {X} = emit{type{X}, '__builtin_expect', X,0}

def for{vars,begin,end,block} = {
  def e = promote{usize,end}
  i:usize = begin
  while (i<e) {
    exec{i, vars,block}
    i = i+1
  }
}
def for_const{vars,begin,end,block} = {
  if (0<end) {
    for_const{vars,begin,end-1,block}
    exec{end-1, vars,block}
  }
}
def for_unroll{unr}{vars,begin,end,block} = {
  def e = promote{usize,end}
  i:usize = begin
  eu := e & ~cast{usize,unr-1}
  while (i<eu) {
    @for_const (j to unr) exec{i+j, vars,block}
    i += unr
  }
  while (i<e) {
    exec{i, vars,block}
    i += 1
  }
}

def print{x:T & match{'number',kind{T}}} = emit{void,'printf','"%d\n"',x}
def print{x:u64} = emit{void,'printf','"%ld\n"',x}
