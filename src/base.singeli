# Base definitions that determine how "our version" of Singeli works

# Mostly it looks like C
include 'arch/c'    # Backend
include 'skin/c'    # Operators
include 'skin/cext'

include 'util/tup'  # List utilities: scan and so on
include 'util/kind' # Kind-test functions like ktup
def range = iota

oper <-> swap        infix none  20
oper >*  ptr_gt      infix none  20
oper *?  lazy_load   prefix      60
oper *?  lazy_load   infix right 50

def tptr{T} = 'pointer'===typekind{T}

local def first{t} = tupsel{0,t}
def single{x} = (if (ktup{x}) first{x}; else x)

def swap{a, b} = { t:=a; a=b; b=t }
def swap{a:T, b:T & tptr{T}} = { t := *a; a <- *b; b <- t }

def __pnt{t & ktup{t}} = __pnt{single{t}}
def ptr_gt{a,b} = load{a,0} > load{b,0}

def usize = primtype{'u',width{*void}}

# Change to unsigned: here we always subtract smaller from greater
def __sub{a:P,b:P & tptr{P}}  = emit{usize, 'op -', a, b}

def addtype{a:T} = { if (tptr{T}) usize; else T }
def __add{a, b:u1} = __add{a, addtype{a}^~b}
def __sub{a, b:u1} = __sub{a, addtype{a}^~b}

def clone{old} = { new:=old }

def expect{e}{X} = emit{type{X}, '__builtin_expect', X,e}
def RARE   = expect{0}
def LIKELY = expect{1}

# Pervasion
local include 'util/perv'
extend perv1{clone}
extend perv2{load}
extend (perv{3}){store}
extend perv2{__add}
extend perv2{__sub}
extend perv2{__or }
extend perv2{__shl}
extend perv2{__shr}
extend perv1{__decr}
def eachrec{fn,...a} = { (extend (perv{tuplen{a}}){fn}){...a} }

def extend ecmp{f} = {
  def f{a,b & ktup{a} & ktup{b}} = f{first{a},first{b}}
}
extend ({...f}=>each{ecmp,f}){
  __eq, __ne, __lt, __gt, __le, __ge,
  ptr_gt
}

def type{s & ktup{s}} = type{first{s}}
def scaltype{T} = (if (tptr{T}) scaltype{eltype{T}}; else T)
def scaltype{x & ~ktyp{x}} = scaltype{type{x}}

def isid{ptr} = ~tptr{type{ptr}}
def load{ptr, i & isid{ptr}} = ptr + i
def store{ptr=='sink', i, val} = val
def __add{ptr=='sink', i} = 'sink'

def lazy_load{p} = lazy_load{p,0}
def lazy_load{p,i} = {
  def s = load{single{p},single{i}}
  if (not ktup{p}) s; else {
    def ll{p,i}{} = load{p,i}
    def pr = slice{p,1}
    merge{
      tup{s},
      (if (ktup{i}) each{ll, pr, slice{i,1}}; else each{{p}=>ll{p,i},pr})
    }
  }
}
def store{p, i, val & kgen{val}} = store{p, i, val{}}
def get{a} = a
def get{a & kgen{a}} = a{}
extend perv1{get}

def for{vars,begin,end,block} = {
  def e = usize^~end
  i:usize = begin
  while (i<e) {
    exec{i, vars,block}
    i += 1
  }
}
def for_const{vars,begin,end,block} = {
  if (begin<end) {
    for_const{vars,begin,end-1,block}
    exec{end-1, vars,block}
  }
}
def for_unroll{unr}{vars,begin,end,block} = {
  def e = usize^~end
  i:usize = begin
  eu := e & ~ usize~~(unr-1)
  while (i<eu) {
    @for_const (j to unr) exec{i+j, vars,block}
    i += unr
  }
  while (i<e) {
    exec{i, vars,block}
    i += 1
  }
}
